extends PanelContainer
class_name LoginPanel

signal submit 
signal register
signal error(message:String)

func _ready() -> void:
	%Reg.pressed.connect(func(): register.emit())
	%ShowPasswordButton.toggled.connect(_on_show_password_toggled)
	%ShowPasswordButton.toggle_mode = true
	%ShowPasswordButton.button_pressed = false
	%Username.placeholder_text = "correo@dominio.com"
	%Password.placeholder_text = "Contrase√±a"
	
	# Load saved credentials if they exist
	var credentials = SavedCredentials.load_credentials()
	if credentials.username != "" and credentials.password != "":
		%Username.text = credentials.username
		%Password.text = credentials.password
		%RememberPassword.button_pressed = true

func _on_show_password_toggled(button_pressed: bool) -> void:
	%Password.secret = !button_pressed
	if button_pressed:
		%ShowPasswordButton.text = "üôà"
		%ShowPasswordButton.tooltip_text = "Ocultar contrase√±a"
	else:
		%ShowPasswordButton.text = "üëÅÔ∏è"
		%ShowPasswordButton.tooltip_text = "Mostrar contrase√±a"

func GetUsername() -> String:
	return %Username.text
	
func GetPassword() -> String:
	return %Password.text

func EnableAuthControls() -> void:
	%Submit.disabled = false
	%Reg.disabled = false
	%Username.editable = true
	%Password.editable = true
	
func DisableAuthControls() -> void:
	%Submit.disabled = true
	%Reg.disabled = true
	%Username.editable = false
	%Password.editable = false


func SetCredentials(username: String, password: String) -> void:
	%Username.text = username
	%Password.text = password


func _OnButtonLoginPressed() -> void:
	var email: String = %Username.text.strip_edges()
	if email.is_empty():
		error.emit("Escriba el email de la cuenta")
		return 
		
	if !_is_valid_email(email):
		error.emit("Ingrese un email v√°lido para la cuenta")
		return
	
	if %Password.text.is_empty():
		error.emit("Escriba la contrase√±a de la cuenta")
		return
		
	if %Password.text.length() < 3 || %Password.text.length() > 15:
		error.emit("La contrase√±a de la cuenta tiene que tener un m√≠nimo de 3 caracteres y un m√°ximo de 15 caracteres")
		return
		
	for i in %Password.text:
		if !Utils.LegalCharacter(i):
			error.emit("Contrase√±a inv√°lida: El caracter [{0}] no est√° permitido".format([i]))
			return
			
	Global.username = email
	
	# Save credentials if checkbox is checked
	if %RememberPassword.button_pressed:
		SavedCredentials.save_credentials(%Username.text, %Password.text)
	else:
		SavedCredentials.clear_credentials()
	
	submit.emit()


func _is_valid_email(email: String) -> bool:
	if email.is_empty():
		return false
	if !email.contains("@"):
		return false
	var parts := email.split("@")
	if parts.size() != 2:
		return false
	if parts[0].is_empty() or parts[1].is_empty():
		return false
	if parts[1].find(".") == -1:
		return false
	return true
